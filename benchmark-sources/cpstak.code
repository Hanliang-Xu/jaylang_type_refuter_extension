# EXPECT-WELL-FORMED
# EXPECT-EVALUATE
# EXPECT-ANALYSIS-STACK-IS 1ddpa
# EXPECT-ANALYSIS-LOOKUP-FROM-END r {int}
# EXPECT-ANALYSIS-NO-INCONSISTENCIES

# (define (cpstak x y z)
#
#   (define (tak x y z k)
#     (if (not (< y x))
#         (k z)
#         (tak (- x 1)
#              y
#              z
#              (lambda (v1)
#                (tak (- y 1)
#                     z
#                     x
#                     (lambda (v2)
#                       (tak (- z 1)
#                            x
#                            y
#                            (lambda (v3)
#                              (tak v1 v2 v3 k)))))))))
#
#   (tak x y z (lambda (a) a)))
#
# (cpstak 32 15 8)

ycombinator = fun ycombinatorf -> (
  ycombinatorfx1 = fun ycombinatorx1 -> (
    ycombinatorfv1 = fun ycombinatorv1 -> (
      ycombinatorxx1 = ycombinatorx1 ycombinatorx1;
      ycombinatorfv1r = ycombinatorxx1 ycombinatorv1;
    );
    ycombinatorfx1r = ycombinatorf ycombinatorfv1;
  );

  ycombinatorfx2 = fun ycombinatorx2 -> (
    ycombinatorfv2 = fun ycombinatorv2 -> (
      ycombinatorxx2 = ycombinatorx2 ycombinatorx2;
      ycombinatorfv2r = ycombinatorxx2 ycombinatorv2;
    );
    ycombinatorfx2r = ycombinatorf ycombinatorfv2;
  );

  ycombinatorr = ycombinatorfx1 ycombinatorfx2;
);

true = {};
false = 0;

not = fun notp -> (
  notr = notp ~ {}
              ? fun notm -> (
                notmr = false;
              )
              : fun nota -> (
                notar = true;
              );
);

one = 1;
thirtytwo = 32;
fifteen = 15;
eight = 8;

cpstak = fun cpstakparams -> (
  cpstakx = cpstakparams.x;
  cpstaky = cpstakparams.y;
  cpstakz = cpstakparams.z;

  takp = fun takr -> (
    takpr = fun takprparams -> (
      x = takprparams.x;
      y = takprparams.y;
      z = takprparams.z;
      k = takprparams.k;

      yx = y < x;
      notyx = not yx;
      kr = notyx ~ {}
                 ? fun notyxm -> (
                   notyxmr = k z;
                 )
                 : fun notyxa -> (
                   x1 = x - 1;
                   fv1 = fun v1 -> (
                     y1 = y - 1;
                     fv2 = fun v2 -> (
                       z1 = z - 1;
                       fv3 = fun v3 -> (
                         takfinalparams = {x = v1, y = v2, z = v3, k = k};
                         fv3r = takr takfinalparams;
                       );
                       takzparams = {x = z1, y = x, z = y, k = fv3};
                       fv2r = takr takzparams;
                     );
                     takyparams = {x = y1, y = z, z = x, k = fv2};
                     fv1r = takr takyparams;
                   );
                   takxparams = {x = x1, y = y, z = z, k = fv1};
                   notyxar = takr takxparams;
                 );
    );
  );
  
  tak = ycombinator takp;
  
  takid = fun a -> (
    takidr = a;
  );

  takparams = {x = cpstakx, y = cpstaky, z = cpstakz, k = takid};
  cpstakr = tak takparams;
);

cpstakparamsxyz = {x = thirtytwo, y = fifteen, z = eight};
r = cpstak cpstakparamsxyz; # => 15
